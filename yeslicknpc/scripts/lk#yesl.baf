/* ================ *
 *  Yeslick Script  *
 * ================ */

/* Kit : Removed as of v3.0: now, kit value is integrated into .cre files by main_component.tpa routine.

IF
	Global("MyKit","LOCALS",0)
	InParty(Myself)
THEN
	RESPONSE #100
		SetGlobal("MyKit","LOCALS",1)
		AddKit("LK#YK")
END
*/


/* ----------- *
 *  Dialogues  *
 * ----------- */
IF
	InParty(Myself)
	Global("LK#YeslickFriendTalks","GLOBAL",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("LK#YeslickFriendTalksTimer","GLOBAL",3600)
		SetGlobal("LK#YeslickFriendTalks","GLOBAL",1)
END

/* It is after Spellhold. Disable the dialogue about why Irenicus would kidnap Imoen if the dialogue didn't happen yet */
IF
	InParty(Myself)
	CombatCounter(0)
	!See([ENEMY])	
	GlobalGT("Chapter","GLOBAL",%bg2_chapter_4%) 
	Global("LK#YeslickFriendTalksImoen","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("LK#YeslickFriendTalksImoen","GLOBAL",3)
END

/* Group landed in Brynnlaw. Now talk about Imoen being kidnapped needs to fire or it will be too late. This is compatible with Imoen 4 Ever */
/* we set a short timer so it doesn't trigger directly after setting foot in Brynnlaw */
IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])	
	GlobalGT("Chapter","GLOBAL",%bg2_chapter_3%) 
	!Global("C#IM_ImoenStays","GLOBAL",1)
	Global("LK#YeslickFriendTalksImoen","GLOBAL",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("LK#YeslickFriendTalksTimer","GLOBAL",600)
		SetGlobal("LK#YeslickFriendTalksImoen","GLOBAL",1)
END

/* activate dialogue about Imoen being kidnapped. If group idles in chapter 3 (and I4E is not installed), this will just fire after Friendship talk 5 */
IF
	InParty(Myself)
	See(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	RealGlobalTimerExpired("LK#YeslickFriendTalksTimer","GLOBAL")
	CombatCounter(0)
	!See([ENEMY])
	OR(2)
		Global("LK#YeslickFriendTalks","GLOBAL",11)
		GlobalGT("Chapter","GLOBAL",%bg2_chapter_3%) 
	!Global("C#IM_ImoenStays","GLOBAL",1)
	GlobalLT("LK#YeslickFriendTalksImoen","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("LK#YeslickFriendTalksImoen","GLOBAL",2)
END

/* trigger dialogue about Imoen being kidnapped */
IF
	InParty(Myself)
	See(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("LK#YeslickFriendTalksImoen","GLOBAL",2)
THEN
	RESPONSE #100
		RealSetGlobalTimer("LK#YeslickFriendTalksTimer","GLOBAL",7200)
		Dialogue(Player1)
END

/* normal timered friendship dialogues */
/* activate dialogues */
IF
	InParty(Myself)
	See(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	RealGlobalTimerExpired("LK#YeslickFriendTalksTimer","GLOBAL")
	CombatCounter(0)
	!See([ENEMY])
	OR(6)
		Global("LK#YeslickFriendTalks","GLOBAL",1)
		Global("LK#YeslickFriendTalks","GLOBAL",3)
		Global("LK#YeslickFriendTalks","GLOBAL",5)
		Global("LK#YeslickFriendTalks","GLOBAL",7)
		Global("LK#YeslickFriendTalks","GLOBAL",9)
		Global("LK#YeslickFriendTalks","GLOBAL",11)
THEN
	RESPONSE #100
		IncrementGlobal("LK#YeslickFriendTalks","GLOBAL",1)
END

/* trigger dialogues */
IF
	InParty(Myself)
	See(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	OR(6)
		Global("LK#YeslickFriendTalks","GLOBAL",2)
		Global("LK#YeslickFriendTalks","GLOBAL",4)
		Global("LK#YeslickFriendTalks","GLOBAL",6)
		Global("LK#YeslickFriendTalks","GLOBAL",8)
		Global("LK#YeslickFriendTalks","GLOBAL",10)
		Global("LK#YeslickFriendTalks","GLOBAL",12)
THEN
	RESPONSE #100
		RealSetGlobalTimer("LK#YeslickFriendTalksTimer","GLOBAL",7200)
		Dialogue(Player1)
END


/* ------------------- *
 *  Yeslick Drow talk  *
 * ------------------- */
IF 
	InParty(Myself)
	See(Player1)
	Global("PCLooksLikeDrow","GLOBAL",1)
	Global("LK#YeslickDrowTalk","GLOBAL",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("LK#YeslickDrowTalkTimer","GLOBAL",600)
		SetGlobal("LK#YeslickDrowTalk","GLOBAL",1)
END

IF 
	InParty(Myself)
	See(Player1)
	!StateCheck("lk#yeslk",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("LK#YeslickDrowTalk","GLOBAL",1)
	GlobalTimerExpired("LK#YeslickDrowTalkTimer","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("LK#YeslickDrowTalk","GLOBAL",2)
END

IF
	InParty(Myself)
	See(Player1)
	!StateCheck("lk#yeslk",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("LK#YeslickDrowTalk","GLOBAL",2)
THEN
	RESPONSE #100
		MoveToObject(Player1)
		Dialogue(Player1)
END


/* -------------------- *
 *  Yeslick Bodhi talk  *
 * -------------------- */
IF 
	InParty(Myself)
	See(Player1)
	Dead("c6bodhi")
	InParty("Imoen2")
	Global("LK#YeslickBodhiTalk","GLOBAL",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("LK#YeslickBodhiTalkTimer","GLOBAL",120)
		SetGlobal("LK#YeslickBodhiTalk","GLOBAL",1)
END

IF 
	InParty(Myself)
	See(Player1)
	!StateCheck("lk#yeslk",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("LK#YeslickBodhiTalk","GLOBAL",1)
	GlobalTimerExpired("LK#YeslickBodhiTalkTimer","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("LK#YeslickBodhiTalk","GLOBAL",2)
END

IF
	InParty(Myself)
	See(Player1)
	!StateCheck("lk#yeslk",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	InParty("Imoen2")
	Global("LK#YeslickBodhiTalk","GLOBAL",2)
THEN
	RESPONSE #100
		MoveToObject(Player1)
		Dialogue(Player1)
END


/* ----------------------------- *
 *  Yeslick Suldanessellar talk  *
 * ----------------------------- */
IF 
	InParty(Myself)
	See(Player1)
	AreaCheck("AR2800")
	Global("LK#YeslickSuldTalk","GLOBAL",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("LK#YeslickSuldTalkTimer","GLOBAL",300)
		SetGlobal("LK#YeslickSuldTalk","GLOBAL",1)
END

IF 
	InParty(Myself)
	See(Player1)
	!StateCheck("lk#yeslk",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("LK#YeslickSuldTalk","GLOBAL",1)
	GlobalTimerExpired("LK#YeslickSuldTalkTimer","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("LK#YeslickSuldTalk","GLOBAL",2)
END

IF
	InParty(Myself)
	See(Player1)
	!StateCheck("lk#yeslk",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("LK#YeslickSuldTalk","GLOBAL",2)
THEN
	RESPONSE #100
		MoveToObject(Player1)
		Dialogue(Player1)
END



/* ------------------ *
 *  Scenery comments  *
 * ------------------ */

// Inside demon plane
// ------------------
IF
	InParty(Myself)
	See(Player1)
	AreaCheck("AR2800")
	Global("LK#YeslickPlane","GLOBAL",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("LK#YeslickPlaneTimer","GLOBAL",30)
		SetGlobal("LK#YeslickPlane","GLOBAL",1)
END

IF 
	InParty(Myself)
	See(Player1)
	!StateCheck("lk#yeslk",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("LK#YeslickPlane","GLOBAL",1)
	GlobalTimerExpired("LK#YeslickPlaneTimer","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("LK#YeslickPlane","GLOBAL",2)
END

IF
	InParty(Myself)
	See(Player1)
	!StateCheck("lk#yeslk",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("LK#YeslickPlane","GLOBAL",2)
THEN
	RESPONSE #100
		MoveToObject(Player1)
		Dialogue(Player1)
END

// After killing Firkraag
// ----------------------
IF
	InParty(Myself)
	See(Player1)
	Dead("firkra02")
	Global("LK#YeslickFirkraag","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("LK#YeslickFirkraag","GLOBAL",1)
END

IF
	InParty(Myself)
	See(Player1)
	!StateCheck("lk#yeslk",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("LK#YeslickFirkraag","GLOBAL",1)
THEN
	RESPONSE #100
		MoveToObject(Player1)
		Dialogue(Player1)
END


// Adalon's quest
// --------------
IF
	InParty(Myself)
	See(Player1)
	Global("PlayerLooksLikeDrow","GLOBAL",1)
	Global("LK#YeslickAdalon","GLOBAL",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("LK#YeslickAdalonTimer","GLOBAL",30)
		SetGlobal("LK#YeslickAdalon","GLOBAL",1)
END

IF 
	InParty(Myself)
	See(Player1)
	!StateCheck("lk#yeslk",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("LK#YeslickAdalon","GLOBAL",1)
	GlobalTimerExpired("LK#YeslickAdalonTimer","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("LK#YeslickAdalon","GLOBAL",2)
END

IF
	InParty(Myself)
	See(Player1)
	!StateCheck("lk#yeslk",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("LK#YeslickAdalon","GLOBAL",2)
THEN
	RESPONSE #100
		MoveToObject(Player1)
		Dialogue(Player1)
END


// letting Solaufein go
// --------------------
IF
	InParty(Myself)
	See(Player1)
	Global("SolaufeinJob","GLOBAL",9)
	Global("LK#YeslickSolaufein","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("LK#YeslickSolaufein","GLOBAL",1)
END

IF
	InParty(Myself)
	See(Player1)
	!StateCheck("lk#yeslk",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("LK#YeslickSolaufein","GLOBAL",1)
THEN
	RESPONSE #100
		MoveToObject(Player1)
		Dialogue(Player1)
END

// bringing good news to the Illithid (The Hidden)
// -----------------------------------------------
IF
	InParty(Myself)
	See(Player1)
	GlobalGT("HiddenJob","GLOBAL",2)
	Global("SawIllithid","GLOBAL",1)
	Global("LK#YeslickAdalon","GLOBAL",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("LK#YeslickHiddenTimer","GLOBAL",30)
		SetGlobal("LK#YeslickHidden","GLOBAL",1)
END

IF 
	InParty(Myself)
	See(Player1)
	!StateCheck("lk#yeslk",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("LK#YeslickHidden","GLOBAL",1)
	GlobalTimerExpired("LK#YeslickHiddenTimer","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("LK#YeslickHidden","GLOBAL",2)
END

IF
	InParty(Myself)
	See(Player1)
	!StateCheck("lk#yeslk",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("LK#YeslickHidden","GLOBAL",2)
THEN
	RESPONSE #100
		MoveToObject(Player1)
		Dialogue(Player1)
END


/* ------------- *
 *  Holy Symbol  *
 * ------------- */
IF
	XPGT("lk#yeslk",3825000)
	Global("LK#YeslickHolySymbol","GLOBAL",0)
THEN
	RESPONSE #100
		ActionOverride("lk#yeslk",CreateItem("lk#ysy1i",0,0,0))
		SetGlobal("LK#YeslickHolySymbol","GLOBAL",1)
END
